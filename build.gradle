buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.12'
    }
}

plugins {
    id 'org.springframework.boot' version '2.3.2.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'java'
    id 'com.google.protobuf' version "0.8.10"
    id "com.avast.gradle.docker-compose" version "0.13.0"
    id "com.palantir.docker" version "0.25.0"
    id "com.palantir.git-version" version "0.12.3"
}

group = 'com.shortener'
version gitVersion()
sourceCompatibility = '14'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'io.github.lognet:grpc-spring-boot-starter:3.5.5'
//    compile 'org.springframework.boot:spring-boot-starter-web'

    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    testImplementation("org.junit.jupiter:junit-jupiter-api")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine")
    testImplementation("org.springframework.boot:spring-boot-starter-test") {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testImplementation 'org.hamcrest:hamcrest:2.2'
}

sourceSets {
    main {
        java {
            srcDir 'src/main/protoGen'
        }
    }
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.5.1'
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:1.30.0"
        }
    }

    generateProtoTasks {
        ofSourceSet('main').each { task ->
            task.builtins {
                java {
                    outputSubDir = 'protoGen'
                }
            }
            task.plugins {
                grpc {
                    outputSubDir = 'protoGen'
                }
            }
        }
    }
    generatedFilesBaseDir = "$projectDir/src/"
}

task cleanProtoGen {
    doFirst {
        delete("$projectDir/src/main/protoGen")
    }
}
clean.dependsOn cleanProtoGen

test {
    useJUnitPlatform {
        excludeTags 'integration'
    }
}

task integrationTest(type: Test, group: 'Verification') {
    useJUnitPlatform {
        includeTags 'integration'
    }
}

dockerCompose {
    useComposeFiles = ['docker/docker-compose.yml']
}

dockerCompose.isRequiredBy(integrationTest)

def dockerRegistry = project.hasProperty('dockerRegistry') ? "$project.dockerRegistry/" : ''
def dockerImage = "$dockerRegistry$name:$version"

docker {
    name dockerImage
    tag 'latest', 'local:latest'
    dockerfile file('Dockerfile')
    files tasks.bootJar.outputs.files
    buildArgs ([ JAR_FILE: bootJar.archiveFileName.get()])
}

docker.dependsOn bootJar
